plugins {
    id 'fabric-loom' version '1.3-SNAPSHOT'
    id 'idea'
}

archivesBaseName = "${mod_name}"
version = "${mc_version}-Fabric-${version}"
group = maven_group

def useLocalYungsApi = findProject(':yungsapi') != null

repositories {
    maven { url "https://maven.fabricmc.net" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven {
        name = 'GeckoLib'
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroupByRegex("software\\.bernie.*")
            includeGroup("com.eliotlash.mclib")
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${mc_version}"
    mappings loom.officialMojangMappings()
    // YUNG's API
    if (useLocalYungsApi) {
        modImplementation files("libs/YungsApi-${yungsapi_mc_version}-Fabric-${yungsapi_version}.jar")
    } else {
        modImplementation "com.yungnickyoung.minecraft.yungsapi:YungsApi:${yungsapi_mc_version}-Fabric-${yungsapi_version}"
    }
    implementation("org.reflections:reflections:0.10.2")
    include("org.reflections:reflections:0.10.2")

    // GeckoLib
//    modImplementation files("libs/geckolib-fabric-1.20.1-${fabric_geckolib_version}.jar")
    modImplementation "software.bernie.geckolib:geckolib-fabric-1.20.1:${fabric_geckolib_version}"

    // Fabric API + Loader
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    // ClothConfig
    modImplementation "me.shedaniel.cloth:cloth-config-fabric:${project.clothconfig_version_fabric}"

    // ModMenu
    modImplementation ("com.terraformersmc:modmenu:${project.modmenu_version_fabric}") { transitive = false }

    compileOnly project(":Common")
}

loom {
    mixin.defaultRefmapName = "${mod_id}.refmap.json"

    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("Fabric/run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("Fabric/run")
        }
    }
}

processResources {
    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version
    inputs.property "yungsapi_version", project.yungsapi_version
    inputs.property "yungsapi_mc_version", project.yungsapi_mc_version
    inputs.property "mc_version", project.mc_version
    inputs.property "fabric_version", project.fabric_version
    inputs.property "fabric_loader_version", project.fabric_loader_version
    inputs.property "mod_description", project.mod_description
    inputs.property "mod_full_name", project.mod_full_name
    inputs.property "mod_id", project.mod_id
    inputs.property "clothconfig_version_fabric", project.clothconfig_version_fabric
    inputs.property "fabric_geckolib_version", project.fabric_geckolib_version

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "yungsapi_version": project.yungsapi_version,
                "yungsapi_mc_version": project.yungsapi_mc_version,
                "fabric_version": project.fabric_version,
                "fabric_loader_version": project.fabric_loader_version,
                "mc_version": project.mc_version,
                "mod_description": project.mod_description,
                "mod_full_name": project.mod_full_name,
                "fabric_geckolib_version": project.fabric_geckolib_version,
                "clothconfig_version_fabric": project.clothconfig_version_fabric,
                "mod_id": project.mod_id
    }
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_name}" }
    }
}